plugins {
	id 'java'
	id 'org.springframework.boot' version '3.5.5'
	id 'io.spring.dependency-management' version '1.1.7'
}

group = 'com.example'
version = '0.0.1-SNAPSHOT'
description = 'Demo project for Spring Boot'

java {
	toolchain {
		languageVersion = JavaLanguageVersion.of(17)
	}
}

configurations { //프로젝트를 빌드할 때 그래들이 설정 프로세서를 사용하도록 설정
	compileOnly {
		extendsFrom annotationProcessor
	}
}


repositories {
	mavenCentral()
}

ext { 
	set('springCloudVersion', "2025.0.0") // 사용할 스프링 클라우드 버전 정의
	set('testcontainersVersion', "1.19.8") // 사용할 테스트 컨테이너 버전을 지정
}

dependencies {
	implementation 'org.springframework.boot:spring-boot-starter-web'
	testImplementation 'org.springframework.boot:spring-boot-starter-test'
	testRuntimeOnly 'org.junit.platform:junit-platform-launcher'
	
	//validation
	implementation 'org.springframework.boot:spring-boot-starter-validation'
	testImplementation 'org.springframework.boot:spring-boot-starter-webflux'

	//config 
	annotationProcessor "org.springframework.boot:spring-boot-configuration-processor"
	
	//config 서버와 연결을 위한 컨피그 클라이언트에 대한 의존성
	implementation 'org.springframework.cloud:spring-cloud-starter-config'

	// 재시도 패턴을 위한 의존성
	implementation 'org.springframework.retry:spring-retry'
	
	// actuator 의존성 추가 -> 새로고침 이벤트를 트리거
	implementation 'org.springframework.boot:spring-boot-starter-actuator'
	
	// JDBC 의존성 
	implementation 'org.springframework.boot:spring-boot-starter-data-jdbc'
	
	// PostgreSQL 의존성
	runtimeOnly 'org.postgresql:postgresql'
	testImplementation 'org.testcontainers:postgresql'
	
	//JPA 
	//implementation 'org.springframework.boot:spring-boot-starter-data-jpa'
	
    // Flyway -> DB 마이그레이션을 위한 방법 -> 밑에 postgre 의존성 필수임..
    implementation 'org.flywaydb:flyway-core'
    implementation "org.flywaydb:flyway-database-postgresql:10.20.1"
    


}


dependencyManagement { 
	imports {
		//스프링 클라우드의 의존성을 관리하기 위한 BOM
		mavenBom "org.springframework.cloud:spring-cloud-dependencies:${springCloudVersion}"
		// 테스트 컨테이너 의존성을 관리하기 위한 BOM
		mavenBom "org.testcontainers:testcontainers-bom:${testcontainersVersion}" 
	}
}



bootRun { //활성화할 프로파일 -> "주의": cmd에서 다음과 같은 명령어로 실행 -> gradlew bootRun
	systemProperty 'spring.profiles.active', 'testdata'

}

bootBuildImage {
	builder = "docker.io/paketobuildpacks/builder-jammy-base"
	imageName = "${project.name}"
	environment = ["BP_JVM_VERSION": "17"]

	//컨테이너 저장소 연결을 설정하기 위한 섹션
	docker {
		// 컨테이너 저장소 인증을 설정하기 위한 섹션 -> 값은 그래들 속성을 통해 전달
		publishRegistry {
			username = project.findProperty("registryUsername")
			password = project.findProperty("registryToken")
			url = project.findProperty("registryUrl")
		}
	}
}


tasks.named('test') {
	useJUnitPlatform()
}
